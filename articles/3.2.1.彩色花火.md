##【processing效果】3.2.1.彩色花火

作者：[CSDN博客](https://blog.csdn.net/liaowang010)

分类： processing创意编程  标签： java

引入列表数据类型array，使用遍历语法。多个气泡实验。

列表索引从左到右,从0开始计数，以此递增；列表索引从右到左，从-1开始倒着计数,以此递减。

input:

```
ParticleSystem ps;  //粒子系统，添加彩色和鼠标移动功能
 
void setup(){
  size(960,540);
  ps = new ParticleSystem(new PVector(width/2,50));
}
 
void draw(){
  background(255);
  ps.addParticle(mouseX,mouseY); //鼠标调整粒子系统
  ps.run();
}
```

input(Particle粒子类):


```
class Particle {
  PVector location;
  PVector velocity;
  PVector acceleration;
  float lifespan;  //时间周期或生命周期，即延迟时间
  
  color particleColor = color(random(255), random(255), random(255), 95);
 
  Particle(float x,float y){
    location = new PVector(x,y);
    velocity = new PVector(random(-1,1),random(-2,0));
    acceleration = new PVector(0,0.05);
    lifespan = 400.0;
  }
 
  void run() {
    update();
    display();
  }
 
  // Method to update position
  void update() {
    velocity.add(acceleration);
    location.add(velocity);
    lifespan -= 1.0;
  }
 
  // Method to display
  void display() {
    stroke(particleColor, lifespan);
    fill(particleColor, lifespan);
    ellipse(location.x,location.y,8,8);
  }
 
  // Is the particle still useful?
  boolean isDead() {
    if (lifespan < 0.0) {
      return true;
    } else {
      return false;
    }
  }
}
```

input(ParticleSystem粒子系统类):

```
class ParticleSystem {
  ArrayList<Particle> particles;
  PVector origin;
 
  ParticleSystem(PVector location) {
    origin = location.copy();
    particles = new ArrayList<Particle>();
  }
 
  void addParticle(float x,float y) {
    particles.add(new Particle(x,y));
  }
 
  void run() {
    for (int i = particles.size()-1; i >= 0; i--) {
      Particle p = particles.get(i);
      p.run();
      if (p.isDead()) {
        particles.remove(i);
      }
    }
  }
}
```

output:

<left>
<img src="https://img-blog.csdnimg.cn/edcba1740cd9497ebaac08c84be709ed.png" width="30%" height="30%" />
Figure 3.8 彩色花火
</left>

output视频：

<video src="https://live.csdn.net/v/300770"></video>

————————————————

备注：原创，转载请附原文链接。